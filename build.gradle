buildscript {
    ext.kotlin_version = '1.3.61'
    ext.exposed_version = '0.17.7'
    ext.mysql_connector_version = '5.1.46'
    ext.kconf_version = '0.22.1'
    ext.kotlinx_coroutines_version = '1.3.3'
    ext.jackson_version='2.10.1'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'
apply plugin: 'java'

group = pluginGroup
version = pluginVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = targetCompatibility
    }
}

repositories {
    google()
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/kotlin/exposed/'
    }
    maven {
        url 'https://dl.bintray.com/kittinunf/maven/'
    }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

test {
    useJUnitPlatform()
}
shadowJar {
    classifier = ""
    relocate 'kotlin', "$pluginGroup.${project.name}.libs.kotlin"
    relocate 'kotlinx', "$pluginGroup.${project.name}.libs.kotlinx"
    relocate 'org.jetbrains.exposed', "$pluginGroup.${project.name}.libs.org.jetbrains.exposed"
    relocate 'org.gjt.mm.mysql', "$pluginGroup.${project.name}.libs.org.gjt.mm.mysql"
    relocate 'com.mysql', "$pluginGroup.${project.name}.libs.com.mysql"
    relocate 'com.uchuhimo.konf', "$pluginGroup.${project.name}.libs.com.uchuhimo.konf"
    relocate 'com.fasterxml.jackson', "$pluginGroup.${project.name}.libs.com.fasterxml.jackson"
    dependencies {
        include(dependency("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"))
        include(dependency("org.jetbrains.exposed:exposed:$exposed_version"))
        include(dependency("mysql:mysql-connector-java:$mysql_connector_version"))
        include(dependency("com.uchuhimo:konf-yaml:$kconf_version"))
        include(dependency("org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"))
        include(dependency("com.fasterxml.jackson.core:jackson-core:$jackson_version"))
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'

    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-api:1.8-SNAPSHOT'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.exposed:exposed:$exposed_version"
    implementation "mysql:mysql-connector-java:$mysql_connector_version"
    implementation "com.uchuhimo:konf-yaml:$kconf_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}
